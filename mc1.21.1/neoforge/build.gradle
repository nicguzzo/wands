plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
	maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
    maven{
        url "https://maven.terraformersmc.com/releases"
    }
    maven {
        url "https://cursemaven.com"
    }
    maven {
        url "https://maven.saps.dev/minecraft"
        content {
            includeGroup "dev.ftb.mods"
        }
    }
    maven {
        url = "https://maven.jamieswhiteshirt.com/libs-release/"
        content {
            includeGroup 'com.jamieswhiteshirt'
        }
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$project.neoforge_version"
	modImplementation "dev.architectury:architectury-neoforge:$project.architectury_version"

    modApi("me.shedaniel.cloth:cloth-config-neoforge:${project.cloth_config_version}")

    if(project.rtree_version!=""){
        modCompileOnly("com.jamieswhiteshirt:rtree-3i-lite-fabric:${project.rtree_version}")
    }
    if(project.opac_forge!=""){
        modCompileOnly("${project.opac_forge}")        { transitive = false }
    }
    if(project.flan_forge!=""){
        modCompileOnly("${project.flan_forge}")        { transitive = false }
    }
    if(project.ftb_chunks_forge!=""){
        modCompileOnly("${project.ftb_chunks_forge}")   { transitive = false }
    }
    if(project.ftb_library_forge!=""){
        modCompileOnly("${project.ftb_library_forge}")  { transitive = false }
    }

    def commonPath = ":${project.parent.name}:common"
    common(project(path: commonPath, configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: commonPath, configuration: 'transformProductionNeoForge')
    
}

processResources {
    inputs.property "version", rootProject.version
    inputs.property "minecraft_version", minecraft_version
    inputs.property "neoforge", neoforge 
    inputs.property "neoforge_loaderVersion", neoforge_loaderVersion 
	inputs.property "architectury_version", architectury_version 
    inputs.property "neoforge_cloth_config", neoforge_cloth_config
    inputs.property "cloth_config_version", cloth_config_version

    filesMatching("META-INF/neoforge.mods.toml") {
        expand ( 
            "version": rootProject.version,
            "minecraft_version": minecraft_version,
            "architectury_version": architectury_version,
            "neoforge": neoforge,
            "neoforge_loaderVersion": neoforge_loaderVersion,            
            "neoforge_cloth_config": neoforge_cloth_config,
            "cloth_config_version": cloth_config_version
        )
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}

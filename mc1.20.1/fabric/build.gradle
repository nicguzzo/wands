plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven{
        url "https://maven.terraformersmc.com/releases"
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-fabric:${architectury_version}"
    modImplementation "com.terraformersmc:modmenu:${modmenu_version}"
    modApi("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    def commonPath = ":${project.parent.name}:common"
    common(project(path: commonPath, configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: commonPath, configuration: 'transformProductionFabric')

    //common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    //shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}

processResources {
    inputs.property "version", rootProject.version
    inputs.property "minecraft_version", minecraft_version
    inputs.property "architectury_version", architectury_version 
    inputs.property "fabric_cloth_config", fabric_cloth_config
    inputs.property "fabric_loader_version", fabric_loader_version
    inputs.property "fabric_api_version", fabric_api_version
    inputs.property "cloth_config_version", cloth_config_version    
    inputs.property "java_version", java_version

    filesMatching("wands.fabric.mixins.json") {
        expand "java_version": java_version
    }
    filesMatching("fabric.mod.json") {
        expand ( 
            "version": rootProject.version,
            "minecraft_version": minecraft_version,
            "architectury_version": architectury_version,
            "fabric_cloth_config": fabric_cloth_config,
            "fabric_loader_version": fabric_loader_version,
            "fabric_api_version": fabric_api_version,
            "cloth_config_version": cloth_config_version,
			"java_version": java_version
        )
    }

}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}

plugins {
    id 'dev.architectury.loom' version '1.13-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id 'systems.manifold.manifold-gradle-plugin' version '0.0.2-alpha'
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
        archivesName = "$rootProject.archives_base_name-$project.name-MC$minecraft_version"
    }

    repositories {
        maven { url "https://maven.nucleoid.xyz/" }
    }

    loom {
        silentMojangMappingsLicense()
    }

    // Load local dev settings if present (local.properties is gitignored)
    def localPropsFile = rootProject.file('local.properties')
    if (localPropsFile.exists()) {
        def localProps = new Properties()
        localProps.load(localPropsFile.newDataInputStream())
        def world = localProps.getProperty('quickPlayWorld')
        def width = localProps.getProperty('windowWidth')
        def height = localProps.getProperty('windowHeight')

        if (project.name == 'fabric' || project.name == 'neoforge') {
            loom.runs.named('client') {
                if (world) {
                    programArgs "--quickPlaySingleplayer", world
                }
                if (width && height) {
                    programArgs "--width", width, "--height", height
                }
            }
        }
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.officialMojangMappings()
        annotationProcessor 'systems.manifold:manifold-preprocessor:2024.1.47'
        testAnnotationProcessor 'systems.manifold:manifold-preprocessor:2024.1.47'
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        // withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21

    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
        it.options.compilerArgs += ['-Xplugin:Manifold']
    }
}


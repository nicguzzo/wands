import java.nio.file.Files
import java.nio.file.Paths
import java.nio.file.LinkOption

def coreSource = file("${project.rootDir}/core/src/main/java")
def compatSource = file("${project.rootDir}/compat/src/main/java")
def coreTextures = file("${project.rootDir}/core/textures")
def coreLang = file("${project.rootDir}/core/lang")

def linkedCore = file("${project.projectDir}/src/main/core-link")
def linkedCompat = file("${project.projectDir}/src/main/compat-link")
def linkedTextures = file("${project.projectDir}/src/main/resources/assets/wands/textures")
def linkedLang = file("${project.projectDir}/src/main/resources/assets/wands/lang")

task createSharedLinks {
    doLast {
        linkedCore.getParentFile().mkdirs()

        def createLink = { File target, File source ->
            source.mkdirs()
            if (Files.exists(target.toPath(), LinkOption.NOFOLLOW_LINKS)){
                //println "Removing existing link: ${target.name}"
                //Files.delete(target.toPath())
                return;
            }
            println "Creating link: $target -> $source"
            
            if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
                // Windows Junction (/J is safer than symbolic links for Java)
                exec { commandLine "cmd", "/c", "mklink", "/J", target.absolutePath, source.absolutePath }
            } else {
                // Linux/Mac Symlink
                exec { commandLine "ln", "-sfn", source.absolutePath, target.absolutePath }
            }
        }

        createLink(linkedCore, coreSource)
        createLink(linkedCompat, compatSource)
        createLink(linkedTextures, coreTextures)
        createLink(linkedLang, coreLang)
    }
}

tasks.named('compileJava').configure {
    dependsOn createSharedLinks
}
tasks.named('processResources').configure {
    dependsOn createSharedLinks
}

sourceSets {
    main {
        java {
            srcDir linkedCore
            srcDir linkedCompat
        }
    }
}

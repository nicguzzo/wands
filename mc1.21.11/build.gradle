plugins {
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    architectury {
        minecraft = minecraft_version
    }
    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
        archivesName = "$rootProject.archives_base_name-$project.name-MC$minecraft_version"
    }

    repositories {
        maven { url "https://maven.nucleoid.xyz/" }
    }

    loom {
        silentMojangMappingsLicense()
    }

    // Load local dev settings if present (local.properties is gitignored)
    def localPropsFile = rootProject.file('local.properties')
    if (localPropsFile.exists()) {
        def localProps = new Properties()
        localProps.load(localPropsFile.newDataInputStream())
        def world = localProps.getProperty('quickPlayWorld')
        def width = localProps.getProperty('windowWidth')
        def height = localProps.getProperty('windowHeight')

        if (project.name == 'fabric' || project.name == 'neoforge') {
            loom.runs.named('client') {
                if (world) {
                    programArgs "--quickPlaySingleplayer", world
                }
                if (width && height) {
                    programArgs "--width", width, "--height", height
                }
            }
        }
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$minecraft_version"
        mappings loom.officialMojangMappings()
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
        //toolchain {
        //    languageVersion = JavaLanguageVersion.of(21)
        //}
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21

        it.options.compilerArgs += ['-Xplugin:Manifold']
        //it.options.compilerArgs += [
        //    '-Xplugin:Manifold',
        //    '-AMC_VERSION=12111',
        //    '-AUSE_CLOTHCONFIG',
        //    //'-AHAS_FLAN',
        //    //'-AHAS_FTB_CHUNKS',
        //    '-AHAS_OPAC',
        //    '-AHAS_GOML'
        //]
        //
        //doFirst { println "Compiler Args for ${project.name}: ${options.compilerArgs}" }
    }
}


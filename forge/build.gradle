plugins {
    id "com.github.johnrengelman.shadow"
    id "me.shedaniel.unified-publishing" version "0.1.+"
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        url "https://cursemaven.com"
    }
    maven {
        url "https://maven.saps.dev/minecraft"
        content {
            includeGroup "dev.ftb.mods"
        }
    }
    maven {
        url = "https://maven.jamieswhiteshirt.com/libs-release/"
        content {
            includeGroup 'com.jamieswhiteshirt'
        }
    }
}
dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "${architectury}:architectury-forge:${rootProject.architectury_version}"
    modApi "me.shedaniel.cloth:cloth-config-forge:${rootProject.cloth_config_version}"
    
    if(rootProject.opac_forge!=""){
        modCompileOnly "${rootProject.opac_forge}"
    }
    if(rootProject.ftb_chunks_forge!=""){
        modCompileOnly("${rootProject.ftb_chunks_forge}") { transitive = false }
    }
    if(rootProject.ftb_library_forge!=""){
        modCompileOnly("${rootProject.ftb_library_forge}") { transitive = false }
    }
    if(rootProject.flan_forge!=""){
        modCompileOnly("${rootProject.flan_forge}") { transitive = false }
    }

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowBundle project(path: ":common", configuration: "transformProductionForge")
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", rootProject.minecraft_version
    inputs.property "architectury_version", rootProject.architectury_version
    inputs.property "forge_cloth_config", rootProject.forge_cloth_config
    inputs.property "cloth_config_version", rootProject.cloth_config_version
    inputs.property "forge", rootProject.forge
    inputs.property "pack_format", rootProject.pack_format
    inputs.property "resource_pack_format", rootProject.resource_pack_format
    inputs.property "data_pack_format", rootProject.data_pack_format
    
    inputs.property "java_version", rootProject.java_version

    filesMatching("META-INF/mods.toml") {
        expand (
            "version": project.version,
            "minecraft_version": rootProject.minecraft_version,
            "architectury_version": rootProject.architectury_version,
            "forge": rootProject.forge,
            "forge_cloth_config": rootProject.forge_cloth_config,
            "cloth_config_version": rootProject.cloth_config_version
        )
    }
    filesMatching("pack.mcmeta") {
        expand(
            "pack_format": rootProject.pack_format,
            "resource_pack_format": rootProject.resource_pack_format,
            "data_pack_format": rootProject.data_pack_format
        )
    }
    filesMatching("wands.mixins.json") {
        expand "java_version": rootProject.java_version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}


unifiedPublishing {
    project {
        gameVersions = ["${rootProject.minecraft_version}"]
        gameLoaders = ["forge"]
        displayName= "Building Wands ${rootProject.mod_version} - ${rootProject.release_type} - forge - ${rootProject.minecraft_version}"
        version = "${rootProject.mod_version}_${rootProject.release_type}_forge_${rootProject.minecraft_version}"
        releaseType = "${rootProject.release_type}"
        var chlog= System.getenv("WANDS_CHANGELOG")
        if (chlog != null) {
           changelog= chlog
        }
        mainPublication tasks.remapJar
        relations {                    
            depends {
                curseforge = "architectury-api" // Optional, project slug
                modrinth = "lhGA9TYQ" // Optional, project slug or id
            }
            depends {
                curseforge = "cloth-config" // Optional, project slug
                modrinth = "9s6osm5g" // Optional, project slug or id
            }
            includes {} // Mark as an included dependency
            optional {
            } // Mark as an optional dependency
            conflicts {} // Mark as a conflicted dependency
        }

        var cfToken = System.getenv("CF_TOKEN")
        if (cfToken != null) {
            curseforge {
                token = cfToken
                id = "363392" // Required, must be a string, ID of CurseForge project
            }
        }

        var mrToken = System.getenv("MODRINTH_TOKEN")
        if (mrToken != null) {
            modrinth {
                //displayName= "Building Wands"
                displayName= "v${rootProject.mod_version} - ${rootProject.release_type} - forge - ${rootProject.minecraft_version}"
                token = mrToken
                id = "XkisZUfp" // Required, must be a string, ID of Modrinth project
            }
        }
    }
}